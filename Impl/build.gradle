apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    shade
    implementation.extendsFrom shade
}

shadowJar {
    configurations = [project.configurations.shade]
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: '*.jar')
    implementation project.parent
    shade(project.parent) {
        exclude module: 'api'
        exclude module: 'fistinapi-api'
        exclude module: 'spigot-api'
    }
    //shade 'net.oneandone.reflections8:reflections8:0.11.7'
    shade 'org.reflections:reflections:0.9.12'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    from project.parent.sourceSets.main.allSource
}

jar {
    from {
        project.parent.sourceSets.main.output
    }
}

artifacts {
    archives sourcesJar, javadocJar, shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            artifact tasks.shadowJar

            pom {
                groupId = project.group
                artifactId = 'fistinframework'
                version = project.version
                name = 'FistinFramework'
                description = 'Framework permettant de standardiser & d\'aider la cr√©ation des mini-jeux FistinNetwork.'
                url = 'https://github.com/FistinNetwork/FistinFramework'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "FistinFramework_maven"
            url = "https://maven.pkg.github.com/FistinNetwork/FistinFramework"
        }
    }
}
