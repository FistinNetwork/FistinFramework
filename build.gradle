allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'fr.fistin'
    version '1.1.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from javadoc
    }

    compileJava {
        targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()

        maven {
            credentials {
                if(System.getenv('USERNAME') != null) {
                    username System.getenv('USERNAME')
                }
                else username "${USERNAME}"

                if(System.getenv('TOKEN') != null) {
                    password System.getenv('TOKEN')
                }
                else password "${TOKEN}"
            }
            name = "FistinAPI_maven"
            url = "https://maven.pkg.github.com/FistinNetwork/FistinAPI"
        }

        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
        }
        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/public"
        }
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                artifactId = 'fistinframework-api'
                version = project.version
                name = 'FistinFramework (API)'
                description = 'Framework (API) permettant de standardiser & d\'aider la cr√©ation des mini-jeux FistinNetwork.'
                url = 'https://github.com/FistinNetwork/FistinFramework'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name "FistinFramework_maven"
            url "https://maven.pkg.github.com/FistinNetwork/FistinFramework"
        }
    }
}

dependencies {
    api 'fr.fistin:fistinapi-api:1.6.0'
    api 'net.luckperms:api:5.2'
    api 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
}
